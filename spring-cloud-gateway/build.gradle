plugins {
    id 'java-library'
    id 'eclipse'
}

repositories {
    maven {
        url 'https://repo1.maven.org/maven2'
    }
    flatDir {
        dirs 'lib'
    }
}

group = 'com.newrelic.instrumentation.lib'
version = '1.2.0-beta-2'

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

dependencies {
    implementation group: 'io.projectreactor', name: 'reactor-core', version: '3.5.8'
    implementation group: 'org.springframework', name: 'spring-web', version: '6.0.11'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-gateway-core', version: '2.2.10.RELEASE'

    implementation group: 'com.newrelic.agent.java', name: 'agent-bridge', version: '8.4.0'
    implementation group: 'com.newrelic.agent.java', name: 'newrelic-weaver-api', version: '8.4.0'
    implementation group: 'com.newrelic.agent.java', name: 'newrelic-api', version: '8.4.0'

    // If using older agent JARs to compile
    implementation fileTree(dir: 'lib', includes: ['*.jar'])

}

jar {
    manifest {
        attributes(
                'Manifest-Version': version,
                'Implementation-Title': group,
                'Implementation-Version': version,
                'Implementation-Vendor-Id': 'Field Instrumentation'
        )
    }
}

task install(dependsOn: jar, type: Copy) {
    description = 'Copies compiled jar to the NEW_RELIC_EXTENSIONS_DIR.'
    group = 'New Relic'

    def extDir = System.getenv("NEW_RELIC_EXTENSIONS_DIR") ?: " "

    from jar
    into extDir
}

install.doFirst {
    def extDir = System.getenv("NEW_RELIC_EXTENSIONS_DIR")
    if (extDir == null) {
        throw new Exception("Must set NEW_RELIC_EXTENSIONS_DIR.")
    }

    if (extDir.startsWith("~" + File.separator)) {
        extDir = System.getProperty("user.home") + extDir.substring(1);
    }

    if (!file(extDir).directory) {
        throw new Exception(extDir + "NEW_RELIC_EXTENSIONS_DIR, set as '" + extDir + "'is not a valid directory.")
    }
}

